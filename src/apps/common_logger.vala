namespace VSGI {

/**
 * CommonLogger: A composite app that generates log entries per request
 * that conform to the Common Log Format generated by Apache and Nginx
 */
public class CommonLogger : Object, Application, CompositeApp {

    /* Not currently used since log is built with StringBuilder */
    private const string FORMAT = "%s - %s [%s] \"%s %s%s %s\" %d %s %0.4f";
    private const string TIME_FORMAT = "%d/%b/%Y:%H:%M:%S %z";

    public Application app { set; get; }

    public CommonLogger(Application? app=null) {
        this.app = app;
    }

    public Response call(Request request) {
        Time now = Time.local((time_t)new DateTime.now_utc().to_unix());
        StringBuilder builder = new StringBuilder();

        builder.append_printf("%s - - [%s] \"%s %s", request.remote_addr,
            now.format(TIME_FORMAT), request.method.to_string(),
            request.full_path());
        if (request.query_string.length != 0)
            builder.append_printf("?%s", request.query_string);
        builder.append_printf(" %s\"", request.protocol.to_string());

        Response response = app.call(request);

        builder.append_printf(" %u ", response.status);

        if (!response.headers.has_key("Content-Length") |
                response.headers["Content-Length"] == "0")
            builder.append_c('-');
        else
            builder.append(response.headers["Content-Length"]);

        log("VSGI.CommonLogger", LogLevelFlags.LEVEL_WARNING, "%s", builder.str);
        return response;
    }

}

}
